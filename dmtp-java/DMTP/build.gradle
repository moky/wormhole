buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        //classpath "com.novoda:bintray-release:0.3.4"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'maven'
    id 'signing'
}

group 'chat.dim'
//version '1.0-SNAPSHOT'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'chat.dim', name: 'TLV', version: '0.0.1'
    compile group: 'chat.dim', name: 'MTP', version: '0.0.1'
    compile group: 'chat.dim', name: 'UDP', version: '0.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.jfrog.bintray'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def gitUrl = 'https://github.com/moky/wormhole.git'
def siteUrl = 'https://github.com/moky/wormhole'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                name 'DMTP'
                description 'Direct Message Transfer Protocol'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id 'moky'
                        name 'Albert Moky'
                        email 'albert.moky@gmail.com'
                        roles {
                            role 'architect'
                        }
                        organization 'DIM Group'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

tasks.withType(Javadoc) {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
        links 'http://docs.oracle.com/javase/7/docs/api'
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

bintray {
    Properties properties = new Properties()
    File file = project.rootProject.file('local.properties')
    if (file != null && file.exists()) {
        properties.load(file.newDataInputStream())
        user = properties.getProperty('bintray.user')
        key = properties.getProperty('bintray.apikey')
    } else {
        user = System.getenv('bintray.user')
        key = System.getenv('bintray.apikey')
    }

    configurations = ['archives']
    pkg {
        userOrg = 'dimchat'
        repo = 'network'
        name = 'dmtp'
        desc = 'Direct Message Transfer Protocol'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty('bintray.gpg.password')
            }
        }
    }
}
